plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version '0.5-SNAPSHOT' apply false
	id 'com.matthewprenger.cursegradle' version "1.4.0"
}

class Globals {
	static def baseVersion = "0.7.15"
	static def mcVersion = "1.16.4"
	static def yarnVersion = "3"
	static def loaderVersion = "0.10.6+build.214"
	static def apiVersion = "0.25.1+build.416-1.16"
	static def changelog = "Fix a possible crash."
}

version = Globals.baseVersion

def getSubprojectVersion() {
	return Globals.baseVersion
}

allprojects {
	def projectName = it.name;
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	group = "fr.catcore"


	dependencies {
		if (projectName == "1.15.2") {
			minecraft "com.mojang:minecraft:1.15.2"
			mappings "net.fabricmc:yarn:1.15.2+build.17:v2"
			modImplementation "net.fabricmc.fabric-api:fabric-api:0.23.0+build.328-1.15"
		} else {
			minecraft "com.mojang:minecraft:$Globals.mcVersion"
			mappings "net.fabricmc:yarn:${Globals.mcVersion}+build.${Globals.yarnVersion}:v2"
			modImplementation "net.fabricmc.fabric-api:fabric-api:${Globals.apiVersion}"
		}
		modCompile "net.fabricmc:fabric-loader:${Globals.loaderVersion}"
	}

	minecraft {
	}

	configurations {
		dev
	}

	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
		maven {
			name = 'Fabric'
			url = 'http://maven.fabricmc.net/'
		}
		maven {
			name = 'SpongePowered'
			url = 'http://repo.spongepowered.org/maven'
		}
		maven {
			name = 'mojang'
			url = 'https://libraries.minecraft.net/'
		}
		maven {
			name = "Jitpack"
			url "https://jitpack.io/"
		}
	}

	jar {
		classifier = "dev"
	}

	afterEvaluate {
		remapJar {
			input = file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar")
			archiveName = "${archivesBaseName}-${version}.jar"
		}

		artifacts {
			dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: jar
		}

		processResources {
			inputs.property "version", project.version

			from(sourceSets.main.resources.srcDirs) {
				include "fabric.mod.json"
				expand "version": project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude "fabric.mod.json"
			}
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
}

subprojects {

	dependencies {
//        testmodCompile sourceSets.main.output
	}

	task remapMavenJar(type: Copy, dependsOn: remapJar) {
		afterEvaluate {
			from("${project.buildDir}/libs/$archivesBaseName-${version}.jar")
			into("${project.buildDir}/libs/")
			rename { String fn -> "$archivesBaseName-${version}-maven.jar" }
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				afterEvaluate {
					artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
						builtBy remapMavenJar
					}
					artifact(sourcesJar) {
						builtBy remapSourcesJar
					}
				}
			}
		}
	}
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
	afterEvaluate {
		input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
		archiveName = "${archivesBaseName}-${version}-maven.jar"
		addNestedDependencies = false
	}
}

subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }

dependencies {
	afterEvaluate {
		subprojects.each {
			compile project(path: ":${it.name}", configuration: "dev")
			include project("${it.name}:")
		}
	}
}

curseforge {
	apiKey = System.getenv('CURSEKEY') ?: "a key"
	project {
		id = "318315"
		releaseType = "release"
		changelog = Globals.changelog
		mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
			displayName = Globals.baseVersion
			relations {
				requiredDependency "fabric-api"
			}
		}

		gameVersionStrings.clear()
		gameVersionStrings.add("Fabric")

		// 1.16.x
		gameVersionStrings.add("1.16.3")
		gameVersionStrings.add("1.16.2")
		gameVersionStrings.add("1.16.1")
		gameVersionStrings.add("1.16")
		gameVersionStrings.add("1.16-Snapshot")

		// 1.15.x
		gameVersionStrings.add("1.15.2")
		gameVersionStrings.add("1.15.1")
		gameVersionStrings.add("1.15")
		gameVersionStrings.add("1.15-Snapshot")

		// 1.14.x
		gameVersionStrings.add("1.14.4")
		gameVersionStrings.add("1.14.3")
		gameVersionStrings.add("1.14.2")
		gameVersionStrings.add("1.14.1")
		gameVersionStrings.add("1.14")
		gameVersionStrings.add("1.14-Snapshot")
	}
	options{
		forgeGradleIntegration = false
	}
}
